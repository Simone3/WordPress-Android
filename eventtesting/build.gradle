buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
}

dependencies {
    compile('commons-lang:commons-lang:2.6') {
        exclude group: 'commons-logging'
    }
    compile 'com.mcxiaoke.volley:library:1.0.18'
    compile 'com.android.support:support-v13:23.4.0'

    // The event-based testing library
    compile 'it.polimi.testing:event-based-testing:1.0'

    // RxJava observables
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
}

android {
    useLibrary 'org.apache.http.legacy'

    publishNonDefault true

    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        versionName "1.10.0"
        minSdkVersion 14
        targetSdkVersion 23
    }
}

version android.defaultConfig.versionName
group = "org.wordpress"
archivesBaseName = "event_testing"

signing {
    required {
        has("release") && project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'WordPress-EventTesting-Android'
                packaging 'aar'
                description 'Event Testing lib for WordPress-Android'
                url 'https://github.com/wordpress-mobile/WordPress-EventTesting-Android'
                scm {
                    connection 'scm:git:https://github.com/wordpress-mobile/WordPress-EventTesting-Android.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/WordPress-EventTesting-Android.git'
                    url 'https://github.com/wordpress-mobile/WordPress-UEventTesting-Android'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'simoneg'
                        name 'Simone Graziussi'
                        email 'simone.graziussi@gmail.com'
                    }
                }
            }
        }
    }
}

android.libraryVariants.all { variant ->

    task("generate${variant.name}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files, android.getBootClasspath())

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
        }
        exclude '**/R.java'
    }
}
